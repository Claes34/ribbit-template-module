module Template

hidden const pathRegex = Regex(#"(\/)?([^\/\\]+(\/)?)+"#)
hidden const variableRegex = Regex("[a-zA-Z0-9_-]*")

/// name of the template
name: String(!isEmpty)

/// description of the template
description: String(!isEmpty)

/// path of the template
path: String(!isEmpty && matches(pathRegex))

/// Listing of relative path to exclude from template when generating a project.
/// Wildcard capable.
exclude: Listing<String(!isEmpty && matches(pathRegex))> = Listing()

/// Listing of variable collectors
variableCollectors: Listing<VariableCollector> = Listing()

/// Listing of dependencies for project generation
dependencies: Listing<Dependency> = Listing()

/// Listing of OccurencesReplacementsFile, one for each file where occurrences replacements are necessary
occurencesReplacementsFiles: Listing<OccurencesReplacementsFile> = Listing()

/// Listing of MoveFileOrFolder, defines all files that needs to be moved/renamed
mvFilesOrFolders: Listing<MoveFileOrFolder> = Listing()

/// Listing of PostGenerationStep, custom script based steps executed after project generation is done.
postGenerationSteps: Listing<PostGenerationStep> = Listing()

// ##### CLASSES #####

class Command {
    command: String(!isEmpty)
    allowUserInput: Boolean = false
}

class VariableCollector {
    /// name of the variable
    variable: String(!isEmpty && matches(variableRegex))
    prompt: String
    hint: String?
    regex: String(!isEmpty)?
    defaultValue: String?
    choices: List<String(!isEmpty)>(!isEmpty)? = null
}

class Dependency {
    name: String(!isEmpty)
    checkCmd: Command
    installCmd: Command?
    predicate: String(!isEmpty)?
    mandatory: Boolean = false
}

class Replacement {
    input: String(!isEmpty)
    output: String
    predicate: String(!isEmpty)?
}

class OccurencesReplacementsFile {
    filePath: String(!isEmpty && matches(pathRegex))
    replacements: Listing<Replacement>
}

class MoveFileOrFolder {
    inputPath: String(!isEmpty && matches(pathRegex))
    outputPath: String(!isEmpty && matches(pathRegex))
    isFolder: Boolean
    predicate: String(!isEmpty)?
}

class PostGenerationStep {
    name: String
    description: String?
    cmd: Command
    execInProjectFolder: Boolean
    predicate: String(!isEmpty)?
    userConsent: String(!isEmpty)?
    successMandatory: Boolean = false
}